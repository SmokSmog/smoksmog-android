apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'de.felixschulze.gradle.spoon'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "pl.malopolska.smoksmog"
        minSdkVersion 16
        //noinspection OldTargetApi // TODO since it's not tested against new permission system we keep 22
        targetSdkVersion 22
        versionCode 191002
        versionName "1.9.1"

        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {

        debug {
            storeFile file('debug.keystore')
        }

        release {
            storeFile new File("" + System.getenv("KEYSTORE"))
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {

        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
        }

        release {
            if( System.getenv("KEYSTORE") != null ) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

//noinspection GroovyAssignabilityCheck
configurations.all {
    resolutionStrategy {
        force 'com.android.support:support-annotations:23.0.0'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile project(':network')

    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:support-annotations:23.1.1'
    compile 'com.android.support:design:23.1.1'

    compile 'com.google.android.gms:play-services-location:8.3.0'

    compile 'uk.co.chrisjenx:calligraphy:2.1.0'

    compile 'com.jakewharton:butterknife:7.0.1'

    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'com.trello:rxlifecycle:0.4.0'
    compile 'com.trello:rxlifecycle-components:0.4.0'

    compile 'pl.charmas.android:android-reactive-location:0.8@aar'

    compile 'com.google.dagger:dagger:2.0.2'

    apt 'com.google.dagger:dagger-compiler:2.0.2'

    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile 'com.github.PhilJay:MPAndroidChart:v2.1.6'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }

    testCompile 'junit:junit:4.12'

    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.mockito:mockito-core:2.0.31-beta'
    androidTestCompile('com.android.support.test:runner:0.4.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile 'com.squareup.spoon:spoon-client:1.2.0'
    androidTestCompile 'com.squareup.assertj:assertj-android:1.1.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.1') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
    androidTestCompile 'org.apache.directory.studio:org.apache.commons.io:2.4'
}

retrolambda {
    jdk System.getenv("JAVA8_HOME")
    oldJdk System.getenv("JAVA7_HOME")
    //noinspection GroovyAssignabilityCheck
    javaVersion JavaVersion.VERSION_1_7
    jvmArgs '-noverify'
}

spoon {
    teamCityLog = true
    debug = true
    failOnFailure = false
}

play {
    jsonFile = new File("" + System.getenv("PLAY_JSON"))
    track = 'beta'
}
